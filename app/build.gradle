import java.security.spec.InvalidKeySpecException

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

kapt {
    useBuildCache = true
}

android {
    compileSdkVersion compileSdkVer
    buildToolsVersion buildToolsVer

    defaultConfig {
        applicationId "net.kibotu.mediagallery.demo"
        namespace "net.kibotu.mediagallery.demo"
        minSdkVersion minSdkVer
        multiDexEnabled true
        targetSdkVersion targetSdkVer
        versionCode isCi ? commitCount() : VERSION_CODE
        versionName isCi ? simpleReleaseVersionName() : VERSION_NAME
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        try {
            debug {
                storeFile file(DEBUG_KEYSYORE_PATH)
                storePassword DEBUG_STORE_PASSWORD
                keyAlias DEBUG_KEYSTORE_ALLIAS
                keyPassword DEBUG_KEY_PASSWORD
            }
        }
        catch (final ignored) {
            throw new InvalidKeySpecException('You should define DEBUG_KEYSTORE_ALLIAS, DEBUG_STORE_PASSWORD, DEBUG_KEY_PASSWORD in gradle.properties.')
        }
        try {
            release {
                storeFile file(RELEASE_KEYSYORE_PATH)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEYSTORE_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        } catch (final ignored) {
            throw new InvalidKeySpecException('You should define RELEASE_KEYSTORE_ALIAS, RELEASE_STORE_PASSWORD, RELEASE_KEY_PASSWORD in gradle.properties.')
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            pseudoLocalesEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled false
            shrinkResources = false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    // region java compile options

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(11))
        }
    }

    // endregion

    // region kotlin compile options

    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += [
                // disable linting for opt-in annotations
                "-Xopt-in=kotlin.RequiresOptIn",
                // disable compose version check
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=true"
        ]
    }

    // endregion

    // region lint options

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false        // true by default
        checkAllWarnings false
        checkReleaseBuilds false
        ignoreWarnings true       // false by default
        quiet true                // false by default
        tasks.lint.enabled = false
    }

    // endregion

    // region adb options

    adbOptions {
        timeOutInMs 10 * 60 * 1000 // 10 minutes
    }

    // endregion

    // region buildFeatures

    buildFeatures {
        viewBinding true
    }

    // endregion
}

dependencies {
    implementation project(":MediaGallery")

    implementation libs.appCompat
    implementation libs.material
    implementation libs.constraintlayout
    implementation libs.androidKtxCore
    implementation libs.timber
    implementation libs.rxpermissions
    implementation libs.rx2Kotlin
    implementation libs.glide
    kapt libs.glideAnnotationsProcessor
}

configurations.all {

    resolutionStrategy.force "androidx.annotation:annotation:1.5.0"
    resolutionStrategy.force "androidx.lifecycle:lifecycle-livedata:2.5.1"
    resolutionStrategy.force "androidx.lifecycle:lifecycle-process:2.4.1"
    resolutionStrategy.force "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"
    resolutionStrategy.force "androidx.recyclerview:recyclerview:1.2.1"
    resolutionStrategy.force "org.jetbrains:annotations:23.0.0"
    resolutionStrategy.force "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
}